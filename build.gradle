/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.1"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

group 'net.soti.go.plugin'

if (hasProperty('ci_build_no')) { // passed in by GOCD
    version = version +'.GO-' + getProperty('ci_build_no')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = rootProject.version
def projectName = rootProject.group + '.' + rootProject.name

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         :  projectName,
        version    : rootProject.version,
        goCdVersion: '17.12.0',
        name       : 'Soti Email Notification plugin pilot',
        description: 'Notification Plugin that find all changes since last successful run.',
        vendorName : 'Soti',
        vendorUrl  : 'https://www.soti.net'
]


// In this section you declare where to find the dependencies of your project
repositories {
    if (hasProperty('internal_maven')) {
        maven {
            url "${internal_maven}"
        }
    }
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'javax.mail', name: 'javax.mail-api', version: '1.5.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.3.0'
    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
}

build.doLast {
    tasks.archiveArtifacts.execute()
}

task archiveArtifacts(type: Zip) {
    from projectDir
    include 'build/**'
    exclude 'build/distributions/**'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            println pluginDesc
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "${artifactory_repository}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
